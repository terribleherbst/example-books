buildscript {
    repositories {
        maven { url "http://oss.jfrog.org/artifactory/repo" }
        jcenter()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:0.9.14'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "idea"
apply plugin: "eclipse"

repositories {
    jcenter()
    maven { url 'http://clinker.netty.io/nexus/content/repositories/snapshots' }
    maven { url "https://oss.jfrog.org/repo" }
    maven { url "https://nexus.codehaus.org/content/repositories/snapshots/" }
    maven { url 'http://repo.spring.io/repo/' }
}

dependencies {
  // springloaded "org.springframework:springloaded:1.2.1.RELEASE"
    springloaded "org.springframework:springloaded:1.2.2.BUILD-SNAPSHOT"

  compile ratpack.dependency("config")
  compile ratpack.dependency("h2")
  compile ratpack.dependency("hikari")
  compile ratpack.dependency("jackson")
  compile ratpack.dependency("remote")
  compile ratpack.dependency("codahale-metrics")
  compile ratpack.dependency("rx")
  compile ratpack.dependency("hystrix")
  compile ratpack.dependency("pac4j")
  compile "org.pac4j:pac4j-http:1.6.0"
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.4.4'

  runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0.1'
  runtime 'org.apache.logging.log4j:log4j-api:2.0.1'
  runtime 'org.apache.logging.log4j:log4j-core:2.0.1'
  runtime 'com.lmax:disruptor:3.3.0'

  testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
      exclude module: "groovy-all"
  }
  testCompile "org.gebish:geb-spock:0.9.3"
  // Required for mocking multi arg constructor e.g. BookService
  testRuntime "org.objenesis:objenesis:1.2"
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.43.1"
  testCompile ratpack.dependency("remote-test")
}

// The Groovy that rest-assured drags in via ratpack-groovy-test is toxic, prevent it from coming in
// This should be fixed upstream in Ratpack
configurations.testCompile.dependencies.find { it.name == "ratpack-groovy-test" }.exclude(group: "org.codehaus.groovy")

idea {
    project {
        jdkName "1.8"
        languageLevel "1.8"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                //configure git support for the project in idea
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

//some CI config
apply from: "gradle/ci.gradle"
